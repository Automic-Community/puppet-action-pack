*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        list_node_group_class_parameters
Test Template     List Node Group Class Parameters Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    NodeGroup         Class
List Class Parameters with valid inputs
                      [Tags]    main_scenarios
                      OK        ${Node_Group}     ${Class_Name}

Assert Behaviour for Non-Existing Node Group
                      NOK       Invalid           ${Class_Name}

Assert Behaviour for Non-Existing Class
                      NOK       ${Node_Group}     Invalid

Assert Node Group Name as Mandatory Field
                      NOK       ${EMPTY}          ${Class_Name}

Assert Class as Mandatory Field
                      NOK       ${Node_Group}     ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "LIST NODE GROUP CLASS PARAMETERS"*******
    Log    *******Connect to AE*******
    Connect AE

List Node Group Class Parameters Template
    [Arguments]    ${OK_NOK}    ${NodeGroup}    ${Class}
    [Documentation]    This test retrieves list of parameters for provided class in a node group.
    ...    -Precondition: To execute this test variables used should be changed accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${NodeGroup}: This field specifies the node group name.
    ...    -${Class}: This field specifies the class whose parameters are to be retrieved.
    Generate String
    Action Create    ${_LIST_CLASS_PARAM_ACTION}
    Connect To Puppet
    Run Keyword If    '${NodeGroup}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_GROUP#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_GROUP#    ${NodeGroup}
    Run Keyword If    '${Classes}' == 'Invalid'    Action Set    &UC4RB_PUP_CLASS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_CLASS#    ${Class}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_PUP_CLASS_PARAM_VARA#    ELSE IF    '${OK_NOK}' == 'NOK'    Action Object Variable Should Not Be Found
    ...    &UC4RB_PUP_CLASS_PARAM_VARA#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_PUP_CLASS_PARAM_COUNT#    ELSE IF    '${OK_NOK}' == 'NOK'    Action Object Variable Should Not Be Found
    ...    &UC4RB_PUP_CLASS_PARAM_COUNT#

Teardown
    Log    *******End Test "LIST NODE GROUP CLASS PARAMETERS"*******
