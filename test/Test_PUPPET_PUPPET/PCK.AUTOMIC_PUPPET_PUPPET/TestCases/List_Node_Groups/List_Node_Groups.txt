*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        list_node_groups
Test Template     List Node Groups Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    NodeName          Filter
List node groups with valid inputs
                      [Tags]    main_scenarios
                      OK        ${Node_Name}      ${Filter}

Assert node name and filter as optional fields
                      OK        ${EMPTY}          ${EMPTY}

Assert behaviour for invalid filter
                      OK        ${Node_Name}      Invalid

Assert behaviour for Non-Existing Node Name
                      OK        Invalid           ${Filter}

*** Keywords ***
Startup
    Log    *******Start Test "LIST NODE GROUPS"*******
    Log    *******Connect to AE*******
    Connect AE

List Node Groups Template
    [Arguments]    ${OK_NOK}    ${NodeName}    ${NodeFilter}
    [Documentation]    This test list classes assigned to node group
    ...    -Precondition: To execute this test variables used should be changed accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${NodeName}: This field specifies the node name to list only linked node groups.
    ...    -${Filter}: This field specifies filter which will be applied on node name. Eg. Dev
    Generate String
    Action Create    ${_LIST_NODE_GROUPS_ACTION}
    Connect To Puppet
    Run Keyword If    '${NodeName}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_NAME#    ${NodeName}
    Run Keyword If    '${NodeFilter}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_FILTER#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_FILTER#    ${NodeFilter}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "LIST NODE GROUPS"*******
