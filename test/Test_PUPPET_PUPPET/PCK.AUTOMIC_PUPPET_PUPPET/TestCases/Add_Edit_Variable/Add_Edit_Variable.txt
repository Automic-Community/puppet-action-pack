*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        add_edit_variable
Test Template     Add Edit Variable Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    NodeGroup         VariableName    VariableValue
Add a variable to a node group
                      [Tags]    main_scenarios
                      OK        ${Node_Group}     Test            testing

Update same variable to a node group
                      OK        ${Node_Group}     Test            Generated

Assert Behavior for Non-Existing Node Group
                      NOK       Invalid           Generated       Generated

Assert Node Group Name as Mandatory Field
                      NOK       ${EMPTY}          Generated       Generated

Assert Variable Name as Mandatory Field
                      NOK       ${Node_Group}     ${EMPTY}        Generated

Assert Variable Value as Mandatory Field
                      NOK       ${Node_Group}     Generated       ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "ADD EDIT VARIABLE"*******
    Log    *******Connect to AE*******
    Connect AE

Add Edit Variable Template
    [Arguments]    ${OK_NOK}    ${NodeGroup}    ${VariableName}    ${VariableValue}
    [Documentation]    This test add's a new or edits an existing variable to node group
    ...    -Precondition: To execute this test variables used should be changed accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${NodeGroup}: This field specifies the node group name to which you want to add or edit the variable.
    ...    -${VariableName}: This field specifies the variable name to add or edit.
    ...    -${VariableValue}: This field specifies value which you want to set for variable.
    Generate String
    Action Create    ${_ADD_EDIT_VARIABLE_ACTION}
    Connect To Puppet
    Run Keyword If    '${NodeGroup}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_GROUP#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_GROUP#    ${NodeGroup}
    Run Keyword If    '${VariableName}' == 'Generated'    Action Set    &UC4RB_PUP_VARA_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_VARA_NAME#    ${VariableName}
    Run Keyword If    '${VariableValue}' == 'Generated'    Action Set    &UC4RB_PUP_VARA_VALUE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_VARA_VALUE#    ${VariableValue}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "ADD EDIT VARIABLE"*******
