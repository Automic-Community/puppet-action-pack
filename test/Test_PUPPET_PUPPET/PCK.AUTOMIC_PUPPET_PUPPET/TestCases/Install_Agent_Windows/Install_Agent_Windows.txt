*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        install_agent_windows
Test Template     Install Agent Windows Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ServerName               MSIPath        PathToInstallAgent    CAServer                 CertName       Environment        StartupMode    AgentAccountUser    AgentAccountPassword    AgentAccountDomain
Install Agent with valid inputs
                      [Tags]    main_scenarios
                      OK        ${Puppet_Server_Name}    ${MSI_Path}    ${EXECDIR}            ${EMPTY}                 ${EMPTY}       ${EMPTY}           Automatic      ${EMPTY}            ${EMPTY}                ${EMPTY}

Assert Path to Install Agent as optional field also providing Cert Name
                      [Tags]    main_scenarios
                      OK        ${Puppet_Server_Name}    ${MSI_Path}    ${EMPTY}              ${Puppet_Server_Name}    testautomic    agent-specified    Manual         ${EMPTY}            ${EMPTY}                ${EMPTY}

Assertion with invalid server name
                      NOK       Invalid                  ${MSI_Path}    ${EMPTY}              ${EMPTY}                 ${EMPTY}       ${EMPTY}           Automatic      ${EMPTY}            ${EMPTY}                ${EMPTY}

Assert behaviour for non-existing directory for MSI File
                      NOK       ${Puppet_Server_Name}    Invalid        ${EMPTY}              ${EMPTY}                 ${EMPTY}       ${EMPTY}           Automatic      ${EMPTY}            ${EMPTY}                ${EMPTY}

Assert behaviour for valid directory without msi file
                      NOK       ${Puppet_Server_Name}    ${EXECDIR}     ${EMPTY}              ${EMPTY}                 ${EMPTY}       ${EMPTY}           Automatic      ${EMPTY}            ${EMPTY}                ${EMPTY}

Assert behaviour for non-existing directory for agent installation
                      NOK       ${Puppet_Server_Name}    ${MSI_Path}    Invalid               ${EMPTY}                 ${EMPTY}       ${EMPTY}           Automatic      ${EMPTY}            ${EMPTY}                ${EMPTY}

Assert Server Name as Mandatory Field
                      NOK       ${EMPTY}                 ${MSI_Path}    ${EXECDIR}            ${Puppet_Server_Name}    TestAutomic    Production         Automatic      automic             automic                 automic

Assert MSI Path as Mandatory Field
                      NOK       ${Puppet_Server_Name}    ${EMPTY}       ${EXECDIR}            ${Puppet_Server_Name}    TestAutomic    Production         Automatic      automic             automic                 automic

*** Keywords ***
Startup
    Log    *******Start Test "INSTALL AGENT WINDOWS"*******
    Log    *******Connect to AE*******
    Connect AE

Install Agent Windows Template
    [Arguments]    ${OK_NOK}    ${ServerName}    ${MSIPath}    ${PathToInstallAgent}    ${CAServer}    ${CertName}
    ...    ${Environment}    ${StartupMode}    ${AgentAccountUser}    ${AgentAccountPassword}    ${AgentAccountDomain}
    [Documentation]    This test installs puppet agent on windows .
    ...    -Precondition: To execute this test variables used should be changed accordingly. MSI Path should be defined where msi file is located.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${ServerName}: This field specifies server name to be used.
    ...    -${MSIPath}: This field specifies path to msi installation file.
    ...    -${PathToInstallAgent}: This field specifies directory where agent is to be installed.
    ...    -${CAServer}: This field specifies hostname where the CA Puppet master server can be reached. Default value is the value of the PUPPET_MASTER_SERVER property.
    ...    -${CertName}: This field specifies node's certificate name. Use only lowercase letters, numbers, periods, and dashes. Default value is node's fully-qualified name.
    ...    -${Environment}: This field specifies node's environment. E.g Staging. Default value is production.
    ...    -${StartupMode}: This field specifies whether the Puppet agent service should run.
    ...    -${AgentAccountUser}: This field specifies user account which the puppet agent service should use.
    ...    -${AgentAccountPassword}: This field specifies the password for the user account.
    ...    -${AgentAccountDomain}: This field specifies the domain of agent's user account.
    Generate String
    Action Create    ${_INSTALL_AGENT_WINDOWS_ACTION}
    Run Keyword If    '${ServerName}' == 'Invalid'    Action Set    &UC4RB_PUP_MASTER_SERVER#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_MASTER_SERVER#    ${ServerName}
    Run Keyword If    '${MSIPath}' == 'Invalid'    Action Set    &UC4RB_PUP_MSI_FILE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_MSI_FILE#    ${MSIPath}
    Run Keyword If    '${PathToInstallAgent}' == 'Invalid'    Action Set    &UC4RB_PUP_INSTALL_DIR#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_INSTALL_DIR#    ${PathToInstallAgent}
    Action Set    &UC4RB_PUP_CA_SERVER#    ${CAServer}
    Action Set    &UC4RB_PUP_CERT_NAME#    ${CertName}
    Action Set    &UC4RB_PUP_NODE_ENV#    ${Environment}
    Action Set    &UC4RB_PUP_STARTUP_MODE#    ${StartupMode}
    Action Set    &UC4RB_PUP_AGENT_USERNAME#    ${AgentAccountUser}
    Action Set    &UC4RB_PUP_AGENT_PASSWORD#    ${AgentAccountPassword}
    Action Set    &UC4RB_PUP_AGENT_DOMAIN#    ${AgentAccountDomain}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_PUP_LOG_FILE#    ELSE IF    '${OK_NOK}' == 'NOK'    Action Object Variable Should Not Be Found
    ...    &UC4RB_PUP_LOG_FILE#

Teardown
    Log    *******End Test "INSTALL AGENT WINDOWS"*******
