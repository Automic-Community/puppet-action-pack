*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        install_agent_unix
Test Template     Install Agent Unix Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Protocol          ServerName               Port
Install Agent with valid inputs
                      [Tags]    main_scenarios
                      OK        https             ${Puppet_Server_Name}    8140

Assert behaviour with invalid protocol
                      NOK       http              ${Puppet_Server_Name}    8140

Assertion with invalid server name
                      NOK       https             Invalid                  8140

Assert behaviour for invalid port
                      NOK       https             ${Puppet_Server_Name}    0000

*** Keywords ***
Startup
    Log    *******Start Test "INSTALL AGENT UNIX"*******
    Log    *******Connect to AE*******
    Connect AE

Install Agent Unix Template
    [Arguments]    ${OK_NOK}    ${Protocol}    ${ServerName}    ${Port}
    [Documentation]    This test installs .
    ...    -Precondition: To execute this test variables used should be changed accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${Protocol}: This field specifies protocol to be used.
    ...    -${ServerName}: This field specifies server name to be used.
    ...    -${Port}: This field specifies port to be used.
    Generate String
    Action Create    ${_INSTALL_AGENT_UNIX_ACTION}
    Action Set    &UC4RB_PUP_MASTER_PRTCL#    ${Protocol}
    Run Keyword If    '${ServerName}' == 'Invalid'    Action Set    &UC4RB_PUP_MASTER_SERVER#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_MASTER_SERVER#    ${ServerName}
    Action Set    &UC4RB_PUP_MASTER_PORT#    ${Port}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "INSTALL AGENT UNIX"*******
