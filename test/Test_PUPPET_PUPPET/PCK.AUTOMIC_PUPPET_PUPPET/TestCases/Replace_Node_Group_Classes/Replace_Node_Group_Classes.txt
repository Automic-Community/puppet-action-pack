*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        replace_node_group_classes
Test Template     Replace Node Group Classes Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    NodeGroup         Classes
Replace node group classes with a class
                      [Tags]    main_scenarios
                      OK        ${Node_Group}     ${Class_Name}

Replace node group classes with multiple classes
                      OK        ${Node_Group}     docker,pe_nginx

Assert Behaviour for multiple classes out of which one invalid class is provided to a node group
                      NOK       ${Node_Group}     docker,pe_nginx,InvalidClassTest

Assert Behaviour for Non-Existing Node Group
                      NOK       Invalid           ${Class_Name}

Assert Behaviour for Non-Existing Class
                      NOK       ${Node_Group}     Invalid

Assert Node Group Name as Mandatory Field
                      NOK       ${EMPTY}          ${Class_Name}

Assert Class as Mandatory Field
                      NOK       ${Node_Group}     ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "REPLACE NODE GROUP CLASSES"*******
    Log    *******Connect to AE*******
    Connect AE

Replace Node Group Classes Template
    [Arguments]    ${OK_NOK}    ${NodeGroup}    ${Classes}
    [Documentation]    This test replace the existing node group classes with specified classes
    ...    -Precondition: To execute this test variables used should be changed accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${NodeGroup}: This field specifies the node group name of which you want to replace the classes.
    ...    -${Classes}: This field specifies the classes which will replace the existing node group classes.
    Generate String
    Action Create    ${_REPLACE_NODE_GROUP_CLASSES_ACTION}
    Connect To Puppet
    Run Keyword If    '${NodeGroup}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_GROUP#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_GROUP#    ${NodeGroup}
    Run Keyword If    '${Classes}' == 'Invalid'    Action Set    &UC4RB_PUP_CLASSES#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_CLASSES#    ${Classes}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "REPLACE NODE GROUP CLASSES"*******
