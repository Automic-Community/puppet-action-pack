*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        list_nodes
Test Template     List Nodes Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    PuppetDBUrl         SkipSSLValidation       Filter
List nodes with valid filter
                      [Tags]    main_scenarios
                      OK        ${Puppet_DB_URL}    ${SkipSSLValidation}    ${Filter}

Assert filter as optional field
                      OK        ${Puppet_DB_URL}    ${SkipSSLValidation}    ${EMPTY}

Assert behaviour for invalid filter
                      OK        ${Puppet_DB_URL}    ${SkipSSLValidation}    Invalid

Assertion with invalid puppet DB url
                      OK        Invalid             ${SkipSSLValidation}    ${Filter}

*** Keywords ***
Startup
    Log    *******Start Test "LIST NODES"*******
    Log    *******Connect to AE*******
    Connect AE

List Nodes Template
    [Arguments]    ${OK_NOK}    ${DBUrl}    ${SSLValidation}    ${NodeFilter}
    [Documentation]    This test list classes assigned to node group
    ...    -Precondition: To execute this test variables used should be changed accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${Puppet_DB_URL}: This field specifies Specify address of the puppetDB API.
    ...    -${SkipSSLValidation}: This field Skips SSL validation if radio button is selected
    ...    -${Filter}: This field specifies filter which will be applied on node name. Eg. Dev
    Generate String
    Action Create    ${_LIST_NODES_ACTION}
    Run Keyword If    '${DBUrl}' == 'Invalid'    Action Set    &UC4RB_PUP_DB_URL#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_DB_URL#    ${DBUrl}
    Action Set    &UC4RB_PUP_SSL_VALIDATION#    ${SSLValidation}
    Run Keyword If    '${NodeFilter}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_FILTER#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_FILTER#    ${NodeFilter}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_PUP_NODE_COUNT#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_PUP_NODE_COUNT#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_PUP_NODE_LIST#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_PUP_NODE_LIST#

Teardown
    Log    *******End Test "LIST NODES"*******
