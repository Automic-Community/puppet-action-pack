*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        list_nodes
Test Template     List Nodes Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Protocol          ServerName               Port    SkipSSLValidation       Filter
List nodes with valid filter
                      [Tags]    main_scenarios
                      OK        http              ${Puppet_Server_Name}    8081    ${SkipSSLValidation}    ${Filter}

Assert filter as optional field
                      OK        http              ${Puppet_Server_Name}    8081    ${SkipSSLValidation}    ${EMPTY}

Assert behaviour for invalid filter
                      OK        http              ${Puppet_Server_Name}    8081    ${SkipSSLValidation}    Invalid

Assertion with invalid server name
                      NOK       http              Invalid                  8081    ${SkipSSLValidation}    ${Filter}

*** Keywords ***
Startup
    Log    *******Start Test "LIST NODES"*******
    Log    *******Connect to AE*******
    Connect AE

List Nodes Template
    [Arguments]    ${OK_NOK}    ${Protocol}    ${ServerName}    ${Port}    ${SSLValidation}    ${NodeFilter}
    [Documentation]    This test list nodes that satisfies the given filter criteria.
    ...    -Precondition: To execute this test variables used should be changed accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${Protocol}: This field specifies protocol to be used.
    ...    -${ServerName}: This field specifies server name to be used.
    ...    -${Port}: This field specifies port to be used.
    ...    -${SkipSSLValidation}: This field Skips SSL validation if "Yes" is selected.
    ...    -${Filter}: This field specifies filter which will be applied on node name. Eg. Dev
    Generate String
    Action Create    ${_LIST_NODES_ACTION}
    Action Set    &UC4RB_PUP_PROTOCOL#    ${Protocol}
    Run Keyword If    '${ServerName}' == 'Invalid'    Action Set    &UC4RB_PUP_SERVER_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_SERVER_NAME#    ${ServerName}
    Action Set    &UC4RB_PUP_PORT#    ${Port}
    Action Set    &UC4RB_PUP_SSL_VALIDATION#    ${SSLValidation}
    Run Keyword If    '${NodeFilter}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_FILTER#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_FILTER#    ${NodeFilter}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK' and '${NodeFilter}' != 'Invalid'    Action Object Variable Should Be Found    &UC4RB_PUP_NODE_LIST#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_PUP_NODE_LIST#
    Run Keyword If    '${OK_NOK}' == 'OK' and '${NodeFilter}' != 'Invalid'    Action Object Variable Count Value Should Be > 0    &UC4RB_PUP_NODE_COUNT#    ELSE    Action Object Variable Count Value Should Be = 0    &UC4RB_PUP_NODE_COUNT#

Teardown
    Log    *******End Test "LIST NODES"*******
