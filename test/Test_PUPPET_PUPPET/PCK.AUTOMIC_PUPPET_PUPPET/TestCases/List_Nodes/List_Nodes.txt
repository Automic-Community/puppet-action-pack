*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        list_nodes
Test Template     List Nodes Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Filter
List node groups with valid filter
                      [Tags]    main_scenarios
                      OK        ${Filter}

Assert filter as optional field
                      OK        ${EMPTY}

Assert behaviour for invalid filter
                      OK        Invalid

*** Keywords ***
Startup
    Log    *******Start Test "LIST NODES"*******
    Log    *******Connect to AE*******
    Connect AE

List Nodes Template
    [Arguments]    ${OK_NOK}    ${NodeFilter}
    [Documentation]    This test list classes assigned to node group
    ...    -Precondition: To execute this test variables used should be changed accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${Filter}: This field specifies filter which will be applied on node name. Eg. Dev
    Generate String
    Action Create    ${_LIST_NODES_ACTION}
    Action Set    &UC4RB_PUP_BASE_URL#    ${Puppet_URL_For_List_Nodes}
    Action Set    &UC4RB_PUP_SSL_VALIDATION#    ${SkipSSLValidation}
    Run Keyword If    '${NodeFilter}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_FILTER#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_FILTER#    ${NodeFilter}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${NodeFilter}' != 'Invalid'    Action Object Variable Should Be Found    &UC4RB_PUP_NODE_COUNT#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_PUP_NODE_COUNT#
    Run Keyword If    '${NodeFilter}' != 'Invalid'    Action Object Variable Should Be Found    &UC4RB_PUP_NODE_LIST#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_PUP_NODE_LIST#

Teardown
    Log    *******End Test "LIST NODES"*******
