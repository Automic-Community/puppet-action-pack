*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        add_new_node
Test Template     Add New Node Template
Library           com.automic.robot.itpa.ItpaLibrary
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    NodeName          NodeGroup        SignCert
Add new node and sign the certificate
                      [Tags]    main_scenarios
                      OK        ${Node_Name}      ${Node_Group}    YES

Add new node without signining the certificate
                      [Tags]    main_scenarios
                      OK        ${Node_Name}      ${Node_Group}    NO

Assertion with invalid node name
                      [Tags]    main_scenarios
                      OK        Invalid           ${Node_Group}    NO

Assertion with invalid node group
                      [Tags]    main_scenarios
                      NOK       ${Node_Name}      Invalid          NO

Assert node name as mandatory Field
                      [Tags]    main_scenarios
                      NOK       ${EMPTY}          ${Node_Group}    NO

Assert node group as mandatory Field
                      [Tags]    main_scenarios
                      NOK       ${Node_Name}      ${EMPTY}         NO

*** Keywords ***
Startup
    Log    *******Start Test "ADD NEW NODE"*******
    Log    *******Connect to AE*******
    Connect AE

Add New Node Template
    [Arguments]    ${OK_NOK}    ${NodeName}    ${NodeGroup}    ${SignCert}
    [Documentation]    This action is used to add a new node to the puppet server and assign it to a node group. These tests will run only on UNIX agent.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK, NOK.
    ...    -${NodeName}: This field specifies the node name you want to add to node group
    ...    -${NodeGroup}: This field specifies the node group name to which you want to add the node to
    ...    -${SignCert}: This field specifies whether user wants to sign the certificate or not
    Generate String
    Action Create    ${_ADD_NEW_NODE_ACTION}
    Connect To Puppet
    Run Keyword If    '${NodeName}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_NAME#    ${NodeName}
    Run Keyword If    '${NodeGroup}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_GROUP#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_GROUP#    ${NodeGroup}
    Action Set    &UC4RB_PUP_SIGN_CERT#    ${SignCert}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success
    Run Keyword If    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "ADD NEW NODE"*******
