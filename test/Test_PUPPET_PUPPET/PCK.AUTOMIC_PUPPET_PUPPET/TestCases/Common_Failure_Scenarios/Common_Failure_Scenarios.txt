*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        common_failure_scenarios
Default Tags
Test Template     Common Failure Scenarios Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    PuppetURL         Username              Password              SkipSSLValidation    NodeName        NodeGroup        SignCert
Assertion with Invalid Puppet Classifier URL
                      [Tags]    main_scenarios
                      NOK       Invalid           ${Puppet_Username}    ${Puppet_Password}    YES                  ${Node_Name}    ${Node_Group}    YES

Assertion with invalid Username
                      NOK       ${Puppet_URL}     Invalid               ${Puppet_Password}    YES                  ${Node_Name}    ${Node_Group}    NO

Assertion with invalid Password
                      NOK       ${Puppet_URL}     ${Puppet_Username}    Invalid               YES                  ${Node_Name}    ${Node_Group}    NO

Assert when certificate is validated and certificate is not present in truststore
                      NOK       ${Puppet_URL}     ${Puppet_Username}    ${Puppet_Password}    NO                   ${Node_Name}    ${Node_Group}    NO

Assert Puppet URL as mandatory field
                      NOK       ${EMPTY}          ${Puppet_Username}    ${Puppet_Password}    YES                  ${Node_Name}    ${Node_Group}    YES

Assert Username as mandatory field
                      NOK       ${Puppet_URL}     ${EMPTY}              ${Puppet_Password}    YES                  ${Node_Name}    ${Node_Group}    YES

Assert Password as mandatory field
                      NOK       ${Puppet_URL}     ${Puppet_Username}    ${EMPTY}              YES                  ${Node_Name}    ${Node_Group}    YES

*** Keywords ***
Startup
    Log    *******Start Test "COMMON FAILURE SCENARIOS"*******
    Log    *******Connect to AE*******
    Connect AE

Common Failure Scenarios Template
    [Arguments]    ${OK_NOK}    ${PuppetURL}    ${Username}    ${Password}    ${SkipSSLValidation}    ${NodeName}
    ...    ${NodeGroup}    ${SignCert}
    [Documentation]    This test executes the common failure scenarios for action pack with add new node action. These tests will run only on UNIX agent.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK, NOK.
    ...    -${PuppetURL}: This field specifies address of the puppet classifier API.
    ...    -${Username}: This field specifies the username for Puppet
    ...    -${Password}: This field specifies the password for Puppet
    ...    -${SkipSSLValidation}: This field specifies whether user wants to skip the SSL certificate validation.
    ...    -${NodeName}: This field specifies the node name you want to add to node group
    ...    -${NodeGroup}: This field specifies the node group name to which you want to add the node to
    ...    -${SignCert}: This field specifies whether user wants to sign the certificate or not
    Generate String
    Action Create    ${_ADD_NEW_NODE_ACTION}
    Run Keyword If    '${PuppetURL}' == 'Invalid'    Action Set    &UC4RB_PUP_BASE_URL#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_BASE_URL#    ${PuppetURL}
    Run Keyword If    '${Username}' == 'Invalid'    Action Set    &UC4RB_PUP_USERNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_USERNAME#    ${Username}
    Run Keyword If    '${Password}' == 'Invalid'    Action Set    &UC4RB_PUP_PASSWORD#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_PASSWORD#    ${Password}
    Action Set    &UC4RB_PUP_SSL_VALIDATION#    ${SkipSSLValidation}
    Action Set    &UC4RB_PUP_NODE_NAME#    ${NodeName}
    Action Set    &UC4RB_PUP_NODE_GROUP#    ${NodeGroup}
    Action Set    &UC4RB_PUP_SIGN_CERT#    ${SignCert}
    Set Overwrite Agent
    Action Execute
    Assert Failure

Teardown
    Log    *******End Test "COMMON FAILURE SCENARIOS"*******