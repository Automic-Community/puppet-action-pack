*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        list_nodes_for_node_group
Test Template     List Nodes For Node Group Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Protocol          ServerName               Port    NodeGroupName
List nodes of node group with valid inputs
                      [Tags]    main_scenarios
                      OK        https             ${Puppet_Server_Name}    8081    ${Node_Group}

Assertion with invalid server name
                      NOK       https             Invalid                  8081    ${Node_Group}

List nodes of node group with invalid node group name
                      NOK       https             ${Puppet_Server_Name}    8081    Invalid

Assert node group name as mandatory field
                      NOK       https             ${Puppet_Server_Name}    8081    ${EMPTY}

Assert server name as mandatory field
                      NOK       https             ${EMPTY}                 8081    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "LIST NODES FOR NODE GROUP"*******
    Log    *******Connect to AE*******
    Connect AE

List Nodes For Node Group Template
    [Arguments]    ${OK_NOK}    ${Protocol}    ${ServerName}    ${Port}    ${NodeGroupName}
    [Documentation]    This test lists all the nodes of the specified node group.
    ...    -Precondition: To execute this test, required variable values should be change in variable.txt file
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${Protocol}: This field specifies protocol to be used.
    ...    -${ServerName}: This field specifies server name to be used.
    ...    -${Port}: This field specifies port to be used.
    ...    -${NodeGroupName}: This field specifies name of the node group of which user wants to fecth the nodes.
    Generate String
    Action Create    ${_LIST_NODES_FOR_NODE_GROUP_ACTION}
    Connect To Puppet
    Action Set    &UC4RB_PUP_PROTOCOL#    ${Protocol}
    Run Keyword If    '${ServerName}' == 'Invalid'    Action Set    &UC4RB_PUP_SERVER_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_SERVER_NAME#    ${ServerName}
    Action Set    &UC4RB_PUP_PORT#    ${Port}
    Run Keyword If    '${NodeGroupName}' == 'Invalid'    Action Set    &UC4RB_PUP_NODE_GROUP#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_PUP_NODE_GROUP#    ${NodeGroupName}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_PUP_NODES_LIST#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_PUP_NODES_LIST#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_PUP_NODES_COUNT#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_PUP_NODES_COUNT#

Teardown
    Log    *******End Test "LIST NODES FOR NODE GROUP"*******
